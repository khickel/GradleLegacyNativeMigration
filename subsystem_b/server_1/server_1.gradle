plugins {
    id 'dev.nokee.cpp-application'
    id 'code-sign'

    id 'glm.installation-manifest-base'
}

repositories {
    maven(RepositoryUtils.systemOpenSsl(project))
}

application(withDefaultApplicationConfiguration())
application { app->

    // TODO(nokeedev): Support base name on the extension
    // Set the executable filename
//    baseName = 'server_1'

    // Add *.cpp to the project
    cppSources.from(fileTree(dir: projectDir, include: '*.cpp'))

    dependencies {
        implementation project(':static_lib')
    }
}

installationManifests {
    debug
    release
}
installationManifests.all {
    def buildType = name
    from(application.variants
            .filter { it.buildVariant.hasAxisOf(application.buildTypes.named(buildType)) }
            .flatMap { it.first().developmentBinary }
            .flatMap {
                if (it.buildable) { return it.linkTask }
                return null // no buildable binaries
            }
            .flatMap { it.linkedFile }) {
        into('bin')
    }
}