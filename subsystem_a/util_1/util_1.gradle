plugins {
    id 'dev.nokee.native-application'
    id 'dev.nokee.cpp-language'
    id 'dev.nokee.c-language'
    id 'code-sign'

    id 'glm.windows-subsystem'
    id 'glm.installation-manifest-base'
}

repositories {
    maven(RepositoryUtils.systemOpenSsl(project))
}

import dev.nokee.language.c.CHeaderSet
import dev.nokee.language.cpp.CppHeaderSet

ext {
    windowsSubsystem = 'console'
}

// Set the project standard C++ compiler/linker options
application(Configure.withDefaultApplicationConfiguration())
application { app->

    // Set the executable filename
    baseName = 'util_1'
    
    // Add *.cpp/*.c to the project
    sources {
        cpp { from(fileTree(dir: '.', include: '*.cpp')) }
        c { from(fileTree(dir: '.', include: '*.c')) }
        configureEach(CppHeaderSet) { from(projectDir) }
        configureEach(CHeaderSet) { from(projectDir) }
    }

    dependencies {
        implementation project(':static_lib')
    }
}

installationManifests {
    create('debug')
    create('release')
}
installationManifests.all {
    def buildType = name
    from(application.variants
            .filter { it.buildVariant.hasAxisOf(application.buildTypes.named(buildType)) }
            .flatMap { it.first().developmentBinary }
            .flatMap {
                if (it.buildable) { return it.linkTask }
                return null // no buildable binaries
            }
            .flatMap { it.linkedFile }) {
        into('bin')
    }
}
